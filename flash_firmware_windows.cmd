@echo off
:: Flash firmware zip generated by Pong Archive
:: (https://github.com/spike0en/Pong_Archive) on pong
::
:: Author: Adithya R (@ghostrider_reborn)
:: Modified by: Spike (@spike0en)

if exist "%~dp0firmware.7z" (
    set fw_zip=%~dp0firmware.7z
) else (
    echo Error: firmware.7z not found!
    goto :exit
)

set fw_path=%~dp0firmware

echo Extracting firmware.7z ...
"%~dp07z.exe" x "%fw_zip%" -o"%fw_path%"

set fastboot=%~dp0platform-tools-windows\fastboot.exe

set partitions=abl aop aop_config bluetooth cpucp devcfg dsp featenabler hyp imagefv keymaster modem multiimgoem multiimgqti qupfw qweslicstore shrm tz uefi uefisecapp xbl xbl_config xbl_ramdump

(for %%p in (%partitions%) do ( 
    echo:
    echo Flashing %%p ...
    "%fastboot%" flash %%p_ab "%fw_path%\firmware-update\%%p.img"
    if ERRORLEVEL 1 (
        echo Error: Flashing %%p failed!
        goto :exit
    )
))

echo:
echo Completed successfully!

:exit
pause
